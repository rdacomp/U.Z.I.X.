;----------------------------------------------------------
;	      CP/M 2.2 emulator for UZI180
;	    extended from AS.C by Doug Braun
;	 Copyright (C) 1998 by Harold F. Bower
;----------------------------------------------------------
; This module may begin on any arbitrary Page (mod 256)
; boundary to ensure that the BIOS jump table is so aligned.
; It provides a minimal CP/M 2.2 functional subset to allow
; applications to execute in an UZI process.  The only
; requirement on CP/M executables is that the first byte in
; the .COM file be a Far Jump (0C3H opcode).  The Bdos code
; originated with AS.C by Doug Braun, but has been converted
; to assembly and extensively modified.		H.F.Bower
;----------------------------------------------------------

.Z80

ASEG
ORG 100H

UZIX	EQU	1
FCN10V1 EQU	0

IF	UZIX

; functions for UZIX
ExitFcn  EQU	11		; Exit process
OpenFcn  EQU	20		; Open File
CloseFcn EQU	6		; Close File
CreatFcn EQU	20		; Create File through OpenFile
LinkFcn  EQU	17		; Rename?
Unlink	 EQU	33		; unlink (delete) file
ReadFcn  EQU	23		; Read File
WriteFcn EQU	36		; WriteFile at Position
SeekFcn  EQU	25		; Seek file (lseek in UZIX !!!)
SyncFcn  EQU	29		; Sync IObuffers
StatFcn  EQU	27		; Stat file
IoctlFcn EQU	15		; IOctl

ELSE

; functions for UZI
ExitFcn  EQU	0		; Exit process
OpenFcn  EQU	1		; Open File
CloseFcn EQU	2		; Close File
CreatFcn EQU	3		; Create File
LinkFcn  EQU	5		; Rename?
Unlink	 EQU	6		; unlink (delete) file
ReadFcn  EQU	7		; Read File
WriteFcn EQU	8		; WriteFile at Position
SeekFcn  EQU	9		; Seek file
SyncFcn  EQU	11		; Sync IObuffers
StatFcn  EQU	15		; Stat file
IoctlFcn EQU	29		; IOctl

ENDIF

; from FCNTL.H

O_CREAT	 EQU 	0100h		; create and open file 
O_TRUNC	 EQU	0200h		; open with truncation
O_NEW	 EQU	0400h		; create only if not exist
O_RDONLY EQU	0
O_WRONLY EQU	1
O_RDWR	 EQU	2
SEEK_SET EQU	0
SEEK_CUR EQU	1
SEEK_END EQU	2

; A couple of CP/M-specific values used in EMU.

fcb	EQU	005CH		; Default CP/M FCB
buff	EQU	0080H		; Default CP/M Buffer

; General-Purpose Ascii Character Equates.

BS	EQU	08H		; Ascii BackSpace
TAB	EQU	09H		; Ascii Horizontal Tab
LF	EQU	0AH		; Ascii Line Feed
CR	EQU	0DH		; Ascii Carriage Return
ESC	EQU	1BH		; Ascii ESCape Char

; TTY Ioctl Functions for UZI180.  Used by EMU and TTYASM.
; !!!!!-- Ensure definitions match those in unix.h --!!!!!

TIOCGETP  EQU	0		; UZI ioctl Return tty Parms
TIOCSETP  EQU	1		; UZI ioctl Set tty Parms
TIOCSETN  EQU	2		; UZI ioctl Return buffer count
TIOCTLSET EQU	9		; UZI ioctl Unique - Disable Ctl Char Proc
TIOCTLRES EQU	10		; UZI ioctl Unique - Enable Ctl Char Proc

; TTY Ioctl Functions for UZIX

TTY_COOKED	EQU	0	; buffered
TTY_RAW 	EQU	1	; unbuffered, wait
TTY_RAW_UNBUFF	EQU	2	; unbuffered, no wait
TTY_RAWCHAR	EQU	3	; unbuffered, no wait, one char

; File Descriptors needed by EMU for Console IO.

STDIN	  EQU   0		; file descriptor value of keyboard
STDOUT	  EQU	1		; file descriptor value of display

.phase 0E300h

; Initialize both FCB entries to blank values

EStart:	LD	HL,fcbDat	; Move initial FCB
	PUSH	HL
	LD	DE,fcb		;  into
	LD	BC,16		;   position
	LDIR
	POP	HL
	LD	C,16		;    init 2nd entry
	LDIR

; Catenate argv[] elements into default buffer

	POP	IX		; Skip argc
	POP	IX		;  Get Ptr to argv[]
	LD	DE,buff+1	; Pt to CP/M Dflt Buffer
	LD	C,0		;  Cnt to 0
	INC	IX		;   Skip Argv[0]
	INC	IX
Cold0:	LD	L,(IX+0)	; Get Ptr to Arg element
	INC	IX
	LD	H,(IX+0)
	INC	IX
	LD	A,H
	OR	L		; End?
	JR	Z,Cold2		; ..exit if Yes
	LD	A,' '		; Add space separator for args
	LD	(DE),A
	INC	DE
	INC	C		;   bump count
Cold1:	LD	A,(HL)
	OR	A		; End of string?
	JR	Z,Cold0		; ..try next if Yes
	CP	'a'		; ensure
	JR	C,NoCap		;  it
	CP	'z'+1		;   is
	JR	NC,NoCap	;    UCase
	AND	5FH
NoCap:	LD	(DE),A		; Move a byte
	INC	HL
	INC	C		;   bump count
	INC	E		;  bump ptr
	JR	NZ,Cold1	; ..get next byte if no bfr ovfl
			;..else 0FF->100H, terminate
	DEC	E		;  (back up for Null-termination)
Cold2:	XOR	A
	LD	(DE),A		;   Null-terminate for safety

	LD	HL,buff		; Pt to count loc'n in buff
	LD	(HL),C		;  save total arg count
	INC	HL		;   advance to 1st char
	LD	DE,fcb+1
	CALL	FilNm		; Get Name/Typ in 1st FCB
	OR	A		;  (set End flag)
	LD	DE,fcb+17	;   (prepare)
	CALL	NZ,FilNm	;    Get Tame/Typ in 2nd FCB if present

	LD	DE,dir
	LD	B,128
	CALL	ZeroDE		; Clear Directory Buffer

	LD	HL,0
	LD	(0003H),HL	; Clear IOBYTE and Default Drive/User

	JP	__bios		; Go to Cold Start setup

; Fill FCB Name.Typ fields with any present data

FilNm:	LD	A,(HL)		; Get char
	INC	HL		;   bump
	OR	A		; End of String?
	RET	Z
	CP	' '		; "Whitespace"?
	JR	Z,FilNm0	; ..jump if Yes
	CP	TAB
	JR	NZ,FilNm1	; ..jump if No
FilNm0:	DEC	C		; Count down total length
	LD	A,C		;  (prepare)
	JR	NZ,FilNm	; ..loop if Not End
	RET			;  ..else Exit showing EOL

FilNm1:	LD	B,8		; Set length of Name field
	PUSH	DE		;   save Ptr to Name[0]
	CALL	FilFl0		;  Get Name
	POP	DE		;   restore Ptr to Name
	OR	A
	RET	Z		; ..return if End-of-Line
	CP	' '
	RET	Z		; ..return if separator
	CP	'.'
	JR	Z,FilNm2	; ..bypass char skip

FilNm3:	LD	A,(HL)
	INC	HL
	OR	A
	RET	Z		; Exit if End of Line
	CP	' '
	RET	Z		;  or End of Field
	CP	'.'
	JR	NZ,FilNm3	; ..loop til End or period

FilNm2:	LD	A,E
	ADD	A,8		; Adjust FCB ptr to type field
	LD	E,A
	LD	B,3
			;..fall thru to get next char..
; Move bytes from (HL) to (DE) for Count in C, Count in B or Ch in {' ','.',0}

FilFld:	LD	A,(HL)		; Get Char
	INC	HL		;   bump ptr
	OR	A		; End of String?
	RET	Z		; ..return if Yes
FilFl0:	CP	'.'		; Period?
	RET	Z
	CP	' '		; Space?
	RET	Z
	LD	(DE),A		; Else Store byte
	INC	DE		;   bump dest ptr
	DEC	C		; End of Input String?
	LD	A,C		;  (prepare)
	RET	Z		; .return End if Yes
	DJNZ	FilFld		; ..loop til field counter ends
	OR	0FFH		; Return flag
	RET

fcbDat:	DEFB	0
	DEFM	'           '
	DEFB	0,0,0,0

;==========================================================
;     Resident Portion of Basic Disk Operating System
;==========================================================
; bdos()
; {
__bdos:	JP	_bdos0
; }

;.....
; BDOS Function Dispatch Table

fcnTbl:	defw	Fcn0		; Warm Boot
	defw	Fcn1		; ConIn
	defw	Fcn2		; ConOut
	defw	Fcn3		; Reader In
	defw	Fcn4		; Punch Out
	defw	Fcn5		; List Output
	defw	Fcn6		; Direct Console IO
	defw	Fcn7		; Get IOBYTE
	defw	Fcn8		; Set IOBYTE
	defw	Fcn9		; WrBuf
	defw	Fcn10		; RdBuf
	defw	Fcn11		; Get Console Status
	defw	Fcn12		; Return Version #
	defw	Fcn13		; Reset Disk Drive
	defw	Fcn14		; Select Disk
	defw	Fcn15		; Open File
	defw	Fcn16		; Close File
	defw	Fcn17		; Search First Occurance
	defw	Fcn18		; Search Next Occurance
	defw	Fcn19		; Delete File
	defw	Fcn20		; Read File
	defw	Fcn21		; Write File
	defw	Fcn22		; Create File
	defw	Fcn23		; Rename File
	defw	Fcn24		; Return Disk Login Vector
	defw	Fcn25		; Return Current Disk
	defw	Fcn26		; Set DMA
	defw	Fcn27		; Get Allocation Map
	defw	Fcn28		; Write Protect Disk
	defw	Fcn29		; Get R/O Vector Address
	defw	Fcn30		; Set File Attributes
	defw	Fcn31		; Get Disk Parameter Table Address
	defw	Fcn32		; Set/Get User Code
	defw	Fcn33		; Read Random
	defw	Fcn34		; Write Random
	defw	Fcn35		; Compute File Size
	defw	Fcn36		; Set Random Record Field in FCB
;	defw	Fcn37		; Reset Multiple Drives
;	defw	null		; (Fcn38 not implemented)
;	defw	Fcn39		; Get Fixed Disk Vector
;	defw	Fcn40		; Write Random
TBLSZ	 EQU  $-fcnTbl
MAXFCN	 EQU  TBLSZ/2

;------------------------------------------------
; bdos0()
; {

_bdos0:	LD	(_arg),DE
	LD	A,C
	LD	(_call),A
	CP	MAXFCN		; Legal Function?
	LD	A,0FFH		;  Prepare Error code
	LD	L,A
	RET	NC		; ..return if Illegal
	LD	(_userSP),SP
	LD	SP,_Bstack
	PUSH	IX
	PUSH	IY
	LD	B,0		; Fcn # to Word
	LD	HL,_bdosX
	PUSH	HL		;  (ret Addr to Stack)
	LD	HL,fcnTbl
	ADD	HL,BC
	ADD	HL,BC		;   Pt to Fcn entry in Table
	LD	A,(HL)
	INC	HL
	LD	H,(HL)
	LD	L,A
	JP	(HL)		; "Call" Function #

_bdosX:	POP	IY
	POP	IX
	LD	SP,(_userSP)
	LD	DE,(_arg)	; Return Orig contents of DE
	LD	A,(_call)
	LD	C,A		; Return Orig contents of C
	LD	A,L
	LD	B,H		; Strict compatibility
	OR	A
	RET
; }

;------------------------------------------------
;         case 0: _exit();			/* Warm Boot */

Fcn0:	JP	WBoot

;------------------------------------------------
;         case 6: if (arg < 0xfe)		/* Direct Console I/O */
;                     goto conout;
;                 else if (arg == 0xfe)
;                     return (ConSt);
;                 else if (ConSt)       /* 0xff */
;                     goto conout;
;                 else return (0);

Fcn6:	LD	A,E		; _arg in DE
	CP	0FEH		; < 0FE ?
	JR	C,Fcn2		; ..jump if Write if Yes
	PUSH	AF
	CALL	BConSt		; Else get Console Status
	POP	AF
	INC	A
	RET	NZ		; ..exit with 0ffh if 0feh
	LD	A,H
	OR	L		; Any char ready?
	RET	Z		; ..exit if Nothing available
			;..else fall thru to Fcn1..
;------------------------------------------------
;         case 1:				/* Console Input */
;         conin:  read (0, &c, 1);
;                 if (c == '\n')
;                     c = '\r';
;                 return (c);

Fcn1:	CALL	BConIn		; Get Char from Bios
Fcn1A:	LD	H,0
	CP	0AH		; \n?
	LD	L,A		;  (prepare for return
	RET	NZ		; ..return if Not
	LD	L,0DH		; Else return CR
	RET

;------------------------------------------------
;         case 3:				/* Reader (Aux) Input */
;         conin:  read (0, &c, 1);
;                 if (c == '\n')
;                     c = '\r';
;                 return (c);

Fcn3:	CALL	AuxIn		; Get Char from Bios
	JR	Fcn1A		; ..exit via common code

;------------------------------------------------
;         case 2:				/* Console Output */
;         conout: if (arg == '\r')
;                     return (0);
;                 c = arg;
;                 write (1, &c, 1);
;                 break;

Fcn2:	LD	C,E		; _arg in DE, need char in C
	JP	BConOu

;------------------------------------------------
;         case 4:				/* Punch (Aux) Output */
;         conout: if (arg == '\r')
;                     return (0);
;                 c = arg;
;                 write (1, &c, 1);
;                 break;

Fcn4:	LD	C,E		; _arg in DE, need char in C
	JP	AuxOut

;------------------------------------------------
;         case 5: if (arg == '\r')		/* List (Prntr) Output */
;                     return (0);
;                 c = arg;
;                 write (2, &c, 1);
;                 break;

Fcn5:	LD	A,E		; _arg in DE
	CP	13		; \r?
	RET	Z
	JP	List		; ..go to Bios

;------------------------------------------------
;         case 9: ptr = (char *)arg;		/* Print '$'-term String */
;                 while (*ptr != '$')
;                 {
;                     if (*ptr != '\r')
;                         write (1, ptr, 1);
;                     ++ptr;
;                 }
;                 break;
				; Enter: DE -> String (arg)
Fcn9:	LD	A,(DE)		; Get char
	INC	DE		;   pt to Next
	CP	'$'		; End?
	RET	Z		; ..quit if Yes
	LD	C,A
	PUSH	DE
	CALL	BConOu
	POP	DE
	JR	Fcn9		; ..loop Til done
	
;------------------------------------------------
;         case 10: rdbuf (arg);
;                  break;
; rdbuf (arg)
; char *arg;
; {
;     int nread;

;     nread = read (0, arg+2, *arg & 0xff);

IF	FCN10V1

Fcn10:	PUSH	DE		;  (save ptr to buffer)
	LD	A,(DE)		; Enter DE -> Buffer
	LD	C,A
	LD	B,0
	PUSH	BC		; cnt (*arg & 0xff)
	INC	DE
	INC	DE
	PUSH	DE		;  arg+2
	LD	C,STDIN
	PUSH	BC		;   0 (stdin)
	LD	HL,ReadFcn	;    UZI Read Fcn #
	PUSH	HL
	RST	8H		;     Execute!
	POP	BC		; Clean Stack
	POP	BC
	POP	BC
	POP	BC
	POP	DE		; Restore Ptr to Buff

;     --nread;                     /* Forget about newline */

	DEC	HL
	LD	A,L

;     arg[nread+2] = '\0';         /* Remove newline */

	ADD	HL,DE
	INC	HL
	INC	HL
	LD	(HL),0

;     arg[1] = nread;

	INC	DE
	LD	(DE),A
; }
	RET

ELSE

Fcn10:
	push	de
	ex	de,hl		; hl - buffer
	ld	e,(hl)		; e - max chars
	inc	hl
	inc	hl
	ld	d,0		; d - char cnt
get:	push	hl
	push	de
	call	BConIn
	pop	de
	pop	hl
	cp	8
	jr	z,del
	cp	7Fh
	jr	z,del
	cp	3
	jp	z,0
	push	hl
	push	de
	push	af
	ld	c,a
	call	BConOu
	pop	af
	pop	de
	pop	hl
	ld	(hl),a
	cp	CR
	jr	z,eol
	ld	a,e
	cp	d
	jr	z,eol1
	inc	hl
	inc	d
	jr	get
del:	ld	a,d
	or	a
	jr	z,get
	push	hl
	push	de
	ld	c,8
	call	BConOu
	ld	c,' '
	call	BConOu
	ld	c,8
	call	BConOu
	pop	de
	pop	hl
	dec	hl
	dec	d
	jr	get
eol:	ld	(hl),0
eol1:	ld	a,d
	pop	de
	inc	de
	ld	(de),a
	ld	hl,0
	ret

ENDIF


;------------------------------------------------
;         case 11: return (ConSt);		/* Get Console Status */

Fcn11:	JP	BConSt

;------------------------------------------------
;         case 12:				/* Return Version # */

Fcn12:	LD	HL,0022H	; Say this is CP/M 2.2
	RET

;------------------------------------------------
;         case 13:				/* Reset Disk Drive */
;              SDma(0x80);
;              break;
Fcn13:
	LD	BC,80h
	JP	BSDma

;------------------------------------------------
;         case 7:				/* Get IO Byte */
;         case 8: break;			/* Set IO Byte */
;         case 14: break;			/* Select Disk
;         case 25: break;			/* Return Current Disk */
;         case 28: break;			/* Write Protect Disk */
;         case 30: break;			/* Set File Attribytes */
;         case 32: break;			/* Get/Set User Code */
Fcn7:
Fcn8:
Fcn14:
Fcn25:				; 0 = Drive A
Fcn28:
Fcn30:
Fcn32:				; Return User 0
;         default: break;
;     }
;     return (0);

Exit0:	LD	HL,0
	RET

;------------------------------------------------
;         case 15: return (openfile (arg));		/* Open File */
; openfile (blk)
; {
;     desc = open (getname (arg), 2);
				; DE -> arg
Fcn15:	CALL	Fcn17		; Does this file exist?
	LD	A,H
	AND	L
	INC	A		; File Not Found (-1)?
	RET	Z		; ..return -1 if File doesn't exist

Open1:	CALL	CkSrch		;  (Close Search File)

;     arg.recno = 0;

	CALL	ZeroCR
	LD	(IY+13),80h	; use S1 as file open flag

	JR	Exit0		;  Return Dir Code for Entry

;.....
; Common File Open Routine.  Used by Read, Write and Search First.
; Enter: DE = File Mode
;	 HL = Ptr to Null-terminated Path String
; Exit : A = 0 if Error, HL = -1
;	     File Descriptor, A <> 0 if Ok

OpenF:	
IF UZIX
	ld	bc,438		; 0666 in octal
	push	BC
ENDIF
	PUSH	DE		; Mode
	PUSH	HL		;  Path
	LD	HL,OpenFcn	;   UZI Open Fcn #
	PUSH	HL
	RST	8H		;    _open (Path, Mode);
	POP	BC		; Clean Stack
	POP	BC
	POP	BC
	LD	A,H
	AND	L
	INC	A		; FF -> 0?
	RET			; ..return (HL=-1/A=0 if Err, HL=fd/A<>0 of Ok)

;------------------------------------------------
;         case 16: return (closefile (arg));		/* Close File */

;     if (close (arg->desc) == -1)

Fcn16:	LD	IY,(_arg)
	LD	(IY+13),0	; clear file open flag

	LD	HL, SyncFcn	; UZI sync function #
	PUSH	HL
	RST	8H		; Execute!
	POP	BC		; Clean Stack

	JP	Exit0		; Return OK

;....
; Close file descriptor

CloseV:	PUSH	DE
	LD	HL,CloseFcn	;  UZI Close Fcn #
	PUSH	HL
	RST	8H		;   Execute!
	POP	BC		; Clean Stack
	POP	BC
	RET

;------------------------------------------------
;         case 17:					/* Search First */

Fcn17:	CALL	CkSrch		; Ensure Search File closed
	LD	HL,'.'		; Open current directory
	LD	(RName),HL	;  store name in Secondary work string
	LD	DE,O_RDONLY	; Open Read-Only
	LD	HL,RName
	CALL	OpenF		;  _open ('.', 0);
	RET	Z		; HL = -1, A = 0 if Can't Open

	LD	(srchFD),HL	; Else Ok, Save File Descriptor
	LD	(curFil),HL	;   Duplicate for Reading
			;..fall thru to read one entry..
;------------------------------------------------
;         case 18: return (255);			/* Search Next */

Fcn18:	LD	HL,(dmaadr)
	LD	(dmaSav),HL	; Save "real" DMA
Fcn18A:	LD	HL,dir+16
	LD	(dmaadr),HL	;  Set DMA for Dir Op'n
	LD	A,ReadFcn	; UZI Read Function #
	LD	DE,16		;  Len of Dir entries
	CALL	RdWrt0		;   Read an Entry
	JR	C,Fcn18E	; Error if Carry Set
	OR	A		; Read Ok?
	JR	Z,Fcn18E	; ..Return HL=-1 if EOF
	CALL	ChkDir		; Else Set Dir to CP/M, Check Match
	OR	A
	JR	NZ,Fcn18A	; ..loop if No Match

	LD	A,(_call)
	CP	15		; Is this a File Open internal Call?
	LD	HL,0		;  (set Success, Index 0)
	JR	Z,Fcn18X	; ..exit now if Yes

	LD	HL,dir		; Else
	LD	DE,(dmaSav)	;  Move Dir Buffer to "real" DMA
	LD	BC,128
	LDIR
	LD	L,B		; Use 0 in BC
	LD	H,C		;   to show Index 0 (success)
	JR	Fcn18X		;  ..exit

Fcn18E:	LD	HL,-1
Fcn18X:	LD	DE,(dmaSav)
	LD	(dmaadr),DE	; Restore "real" DMA Addr
	RET

;------------------------------------------------
;         case 19: return (delete (arg));		/* Delete File */

Fcn19:	CALL	CkSrch		; Ensure Search file closed

;     if (unlink (getname (arg)) == -1)
				; DE -> arg
	CALL	GetNam		;  Parse to String
	PUSH	HL		; String
	LD	HL,Unlink	;  UZI Unlink Fcn #
	PUSH	HL
	RST	8H		;   Execute!
	POP	BC		; Clean Stack
	POP	BC

;         return (255);
;     return (0);

	LD	A,H
	AND	L
	INC	A		; FF->0?
	JP	NZ,Exit0	;  return Ok if No

ExitM1:	LD	HL,-1
	RET


;------------------------------------------------
;         case 33:					/* Read File Random */
;	    readrandom (fcb)
;	    {
;	    /* CAUTION the seek calls MUST be in this order */
;		_seek (f, (int)(fcb+33) % 128, 0);          /* byte  seek */
;		_seek (f, (int)(fcb+33) / 128, 3);          /* block seek */

Fcn33:	CALL	RWprep		; Prepare File for access
	JP	Z,Exit1

	LD	IY,(_arg)
	LD	A,(IY+33)	; Set Record Count from
	LD	(IY+32),A	; Random Record number
	LD	A,(IY+34)	;
	LD	(IY+12),A	;

	CALL	DoRead
	JR	RWEx

;....
DoRead:
	CALL	SkOff		; Seek to Offset (128-byte rec in Block)
	CALL	SkBlk		; Seek to 512-byte Block

	CALL	BRead		; Read 1 Sector

	PUSH	AF
	LD	DE,(curFil)
;;;;	CALL	CloseV		; Close the file
	LD	DE,0
	LD	(curFil),DE
	POP	AF

	RET


;------------------------------------------------
;         case 20: return (readfile (arg));		/* Read File */
; readfile (arg)
; {
;     nread = read (blk->desc, dmaadr, 128);
				; DE -> arg (FCB)
Fcn20:	CALL	RWprep		; Prepare file for access
	JP	Z,Exit1

	CALL	DoRead		; Read 1 Sector

;     arg.recno++;

	PUSH	AF
	CALL	IncCR		; Bump Current Record #
	POP	AF

RWEx:	JP	C,Exit1		; ..Error if Carry Set

;     if (nread == 0)
;         return (0);

	OR	A		; Good Read?
	JP	Z,Exit0		;   exit w/0 if Yes

;     else  return (1)

	JP	Exit1

;------------------------------------------------
;         case 34:					/* Write File Random */
;	    writerandom (fcb)
;	    {
;	    /* CAUTION the seek calls MUST be in this order */
;	        _seek (f, (int)(fcb+33) % 128, 0);          /* byte  seek */
;		_seek (f, (int)(fcb+33) / 128, 3);          /* block seek */

Fcn34:	CALL	RWprep		; Prepare file for access
	JP	Z,Exit1

	LD	IY,(_arg)
	LD	A,(IY+33)	; Set Record Count from
	LD	(IY+32),A	; Random Record number
	LD	A,(IY+34)	;
	LD	(IY+12),A	;

	CALL	DoWrite
	JR	RWEx

;....
DoWrite:
	CALL	SkOff		; Seek to Offset (128-byte rec in Block)
	CALL	SkBlk		; Seek to 512-byte Block

	CALL	BWrit		; Write 1 Sector

	PUSH	AF
	LD	DE,(curFil)
;;;;	CALL	CloseV		; Close the file
	LD	DE,0
	LD	(curFil),DE
	POP	AF

	RET

;------------------------------------------------
;         case 21: return (writefile (arg));		/* Write File */
; writefile (arg)
; {
;     if (write (blk->desc, dmaadr, 128) != 128)

				; DE -> arg (FCB)
Fcn21:	CALL	RWprep		; Prepare file for access
	JP	Z,Exit1

Fcn21A:	CALL	DoWrite		;   Write

;     arg.recno++;

	PUSH	AF
	CALL	IncCR		; Bump Current Record #
	POP	AF

;         return (255);
;     return (0);

	JR	RWEx		; ..exit via Common R/W Code
; }

;------------------------------------------------
;         case 22: return (makefile (arg));		/* Create File */
; makefile (arg)
; {
;     desc = creat (getname (blk), 0666);

Fcn22:	CALL	CkSrch		; Ensure Search file closed
	LD	HL,0666Q	; Own/Grp/Oth are Read/Execute
	PUSH	HL		; DE -> arg
IF UZIX
	ld	hl, O_CREAT+O_WRONLY+O_TRUNC
	push	hl	; /* flag */
ENDIF
	CALL	GetNam		;  This name string
	PUSH	HL
	LD	HL,CreatFcn	;   UZI Creat Fcn #
	PUSH	HL
	RST	8H		;    Execute!
	POP	BC		; Clean Stack
	POP	BC
	POP	BC

;     if (desc == -1)

	LD	A,H
	AND	L
	INC	A		; FF -> 0?

;         return (255);

	RET	Z		; ..return -1 if Yes

;     arg.recno = 0;

	EX	DE,HL
	CALL	CloseV
	JP	Open1

;------------------------------------------------
;         case 23: return (rename (arg));		/* Rename File */
; rename (arg)
; {
;     RName = getname (arg);

Fcn23:	CALL	CkSrch		; Ensure Search file closed
	PUSH	DE		; Save FCB Ptr
	CALL	GetNam		;  parse to UZI String

	LD	HL,FName
	LD	DE,RName
	LD	BC,12
	LDIR			; Copy to Rename string

;     FName = getname (arg+16);

	POP	DE		; DE -> _arg
	LD	HL,16
	ADD	HL,DE		; Offset to New Name
	EX	DE,HL
	CALL	GetNam		;  parse it returning HL -> FName

;     if (link (RName, FName) < 0) {

	PUSH	HL		; New Name
	LD	HL,RName	;  Old Name
	PUSH	HL
	LD	HL,LinkFcn	;   UZI link Fcn #
	PUSH	HL
	RST	8H		;    Execute!
	POP	BC		; Clean Stack
	POP	BC
	POP	BC

;         return (-1);

	JP	C,ExitM1	; Exit w/Err if Bad
;     }
;     if (unlink (RName) < 0) {

	LD	HL,RName	; Old Name
	PUSH	HL
	LD	HL,Unlink	;  UZI unlink Fcn #
	PUSH	HL
	RST	8H		;   Execute!
	POP	BC		; Clean Stack
	POP	BC
	JP	NC,Exit0	;   exit w/0 if Ok

;         unlink (FName);
				; Else remove the new iNode
	LD	HL,FName	; New Name
	PUSH	HL
	LD	HL,Unlink	;  UZI unlink Fcn #
	PUSH	HL
	RST	8H		;   Execute!
	POP	BC		; Clean Stack
	POP	BC

;         return (-1);

	JP	C,ExitM1	;  return -1 if Bad
;     }
;     return (0);

	JP	Exit0		;   else return Ok
; }

;------------------------------------------------
;         case 24: return (1);			/* Return Disk Login Vector */

Fcn24:
Exit1:	LD	HL,1
	RET

;------------------------------------------------
;         case 26: dmaadr = (char *)arg;		/* Set DMA Address */
;                  break;
				; Enter DE = DMA Address
Fcn26:	LD	C,E
	LD	B,D		; Move to Bios Regs
	JP	BSDma		;  Set in Bios & return

;------------------------------------------------
;         case 27: return (-1)			/* Get Allocation Map */
;         case 29: return (-1)			/* Get R/O Vector Address */
Fcn27:
Fcn29:	LD	HL,-1
	RET

;------------------------------------------------
;         case 31: return (&dpb);		/* Get Disk Param Table Addr */

Fcn31:	LD	HL,dpb
	RET
; }

;------------------------------------------------
;         case 35:				/* Return File Size in FCB */
;				   /* Use stat fcn, rounding up to mod-128 */
;	    if (_stat (dname, &statbuf) == 0) {
				; DE -> fcb
Fcn35:	CALL	CkSrch		; Ensure Search file closed
	CALL	GetNam		;  parse to UZI String
	LD	DE,stBuf
	PUSH	DE		; &statbuf
	PUSH	HL		;  dname
	LD	HL,StatFcn	;   UZI stat Fcn #
	PUSH	HL
	RST	8H		;    Execute!
	POP	BC		; Clean Stk
	POP	BC
	POP	BC
	LD	IY,(_arg)
	LD	A,H
	OR	L		; 0?
	JR	NZ,Fcn35X	; ..jump if Bad

;		(int)fcb+33 = ((512 * statbuf.st_size.o_blkno
;	     		      + statbuf.st_size.o_offset)
;			      + 127)
;			      >> 7;
	LD	HL,(stBuf+14)	; Get Offset
	LD	DE,127
	ADD	HL,DE		;  round up to next 128-byte block
	ADD	HL,HL		;   Shift so H has 128-byte blk #
	LD	E,H		;    position in DE
	LD	HL,(stBuf+16)	; Get Block
	ADD	HL,HL		;  * 2
	ADD	HL,HL		;   * 4 for 128-byte Block Count
	ADD	HL,DE		; Now have CP/M Record Size
	LD	(IY+33),L
	LD	(IY+34),H	;  Store in RR fields in FCB
	LD	(IY+35),D	; (D = 0)

;		return (0);

	LD	L,D
	LD	H,D		; HL = 0
	RET

;	    else {
;		(int)fcb+33 = 0;

Fcn35X:	LD	(IY+33),0
	LD	(IY+34),0
	LD	(IY+35),0

;		return (-1);

	LD	HL,-1
;	    }
	RET

;------------------------------------------------
;         case 36:			/* Set Random Record Field in FCB */

Fcn36:	LD	IY,(_arg)
	LD	A,(IY+32)	; Fetch RecNo
	LD	(IY+33),A	;  place in LSB of RR field (r0)
	LD	A,(IY+12)
	LD	(IY+34),A	;  set (r1)
	LD	(IY+35),0	;  Clear Hi byte of RR (r2)

	LD	HL,0		; Return Ok
	RET

;===========================================================
;		  BDos Support Routines
;===========================================================
; char *
; getname (struct fcb *blk)
; {
;     int j;
;     static char name[16];
;     char *p;

;     p = name;
				; Enter: DE -> FCB drive byte
GetNam:	LD	IX,FName	; Dest to string
	EX	DE,HL
	PUSH	HL		;   (save)
	INC	HL		;  adv to 1st char of FN

;     for (j = 0; j < 8; ++j)
;     {
;         if (!blk->name[j] || blk->name[j] == ' ')
;             break;

	LD	B,8
GetN0:	LD	A,(HL)
	INC	HL
	OR	A
	JR	Z,GetN1
	CP	' '
	JR	Z,GetN1

;         *p++ = chlower (blk->name[j]);

	CALL	ChLower
	LD	(IX+0),A
	INC	IX
	DJNZ	GetN0
;     }

GetN1:	POP	HL
	LD	DE,9
	ADD	HL,DE		; Pt to 1st char of FT
	LD	A,(HL)
	CP	' '		; Any Type?
	JR	Z,GetNX		; ..quit if Not

;     *p++ = '.';

	LD	(IX+0),'.'
	INC	IX

;     for (j = 0; j < 3; ++j)

	LD	B,3

;     {
;         if (!blk->ext[j] || blk->ext[j] == ' ')
;             break;

GetN2:	LD	A,(HL)
	INC	HL
	CP	' '
	JR	Z,GetNX

;         *p++ = chlower (blk->ext[j]);

	CALL	ChLower
	LD	(IX+0),A
	INC	IX
	DJNZ	GetN2

;     }
;     *p = '\0';

GetNX:	LD	(IX+0),0

;     return (name);

	LD	HL,FName
	RET
; }

;.....
; Seek Offset.  Uses Record Count to set 128-byte offset in 512-byte Sctr.

SkOff:	LD	BC,0
	PUSH	BC		; 0 Mode (Absolute Offset Position)
	LD	IY,(_arg)	; Pt to current fcb
	LD	A,(IY+32)	; Fetch Lo Byte of Offset
	AND	03H		;  (kill all but mod 512, 2 bits)
	LD	B,A
	SRL	B
	RR	C		;   Offset is pos'n within 512-byte Rec
	JR	SeekV		;  ..finish in Common Code

;.....
; Seek Block.  Uses Record Count to set Div-512 Block Number

SkBlk:	LD	HL,3		; 3 Mode (Absolute Block Position)
	PUSH	HL		;  to Stk
	LD	IY,(_arg)
	LD	C,(IY+32)
	LD	B,(IY+12)
	SRL	B
	RR	C
	SRL	B
	RR	C		;   Blk # is DIV 512
SeekV:	PUSH	BC
	LD	HL,(curFil)	;    fd
	PUSH	HL
	LD	HL,SeekFcn	;     UZI Seek Fcn #
	PUSH	HL
	RST	8H		;      Execute!
	POP	BC
	POP	BC
	POP	BC
	POP	BC
	RET

;.....
; Perform File Access Preparatory actions:
; Open file for R/W and Seek to current Record #
; Enter: DE = Ptr to FCB
; Exit : A = 0 and HL = -1 if Error, A <> 0 if Ok

RWprep:	CALL	CkSrch		; Ensure Search file closed
	LD	HL,13		; offset to S1 (file open flag)
	ADD	HL,DE
	LD	A,(HL)
	AND	80h
	LD	HL,-1
	RET	Z

	CALL	GetNam		;  Parse FCB Fn.Ft to String

	CALL	COpen
	RET	Z		; ..return -1 on error

	LD	(curFil),HL	; store file descriptor for Bios
	RET

COpen:	PUSH	HL
	LD	DE,CName
chk:	LD	A,(DE)
	CP	(HL)		; compare filename with cached name
	JR	NZ,differ
	OR	A
	JR	Z,same
	INC	HL
	INC	DE
	JR	chk
same:	POP	DE		; if same, just return the cached file descr
	LD	HL,(Cfd)
	LD	A,H
	AND	L
	INC	A
	RET	NZ
	EX	DE,HL
	JR	op1
differ:	LD	HL,(Cfd)
	LD	A,H
	AND	L
	INC	A
	EX	DE,HL
	CALL	NZ,CloseV	; close old file
	POP	HL		; restore file name
	CALL	Ccopy
op1:	LD	DE,O_RDWR	; open for R/W
	CALL	OpenF
	LD	(Cfd),HL
	RET

Ccopy:	PUSH	HL
	LD	DE,CName
cpy:	LD	A,(HL)
	LD	(DE),A
	INC	HL
	INC	DE
	OR	A
	JR	NZ,cpy
	POP	HL
	RET

;.....
; Convert UZI Directory Entry at dir+16 to CP/M FCB entry at dir, Zero rest.
; Ambiguously compare FCB FN.FT at dir to that passed at arg, returning Zero
; if Match, Non-Zero if mismatch.

ChkDir:	LD	DE,dir
	LD	HL,dir+16+2	; Pt to 1st char of Name
	XOR	A
	LD	(DE),A		; Zero Drive field
	INC	DE		;  Pt to 1st char of FN
	LD	B,8
	CALL	ChkD0		;   Fix Name
	LD	B,3
	CALL	ChkD0		;    & Type
	LD	B,21
	CALL	ZeroDE		;     Clear rest of Dir entry

	LD	DE,(_arg)
	INC	DE		; Pt to 1st char of FN
	LD	A,(DE)
	CP	' '		; Any Name present?
	JR	NZ,ChkFN0	; ..jump if Yes
	LD	HL,8
	ADD	HL,DE		;  Else offset to 1st char of FT
	LD	A,(HL)
	CP	' '		;   Type present?
	LD	A,0FFH		;    (Assume Error)
	RET	Z		;     Return w/Err Flag if no Type either

ChkFN0:	LD	HL,dir+1	; Else Compare name/type fields
	LD	B,11
			; Ambiguous FN.FT compare of (HL) to (DE)
ChkL:	LD	A,(DE)
	CP	'?'		; Accept anything?
	JR	Z,ChkL0		; ..jump if ambiguous
	XOR	(HL)
	AND	7FH		; Match?
	RET	NZ		; .Return Non-Zero if Not
ChkL0:	INC	HL
	INC	DE
	DJNZ	ChkL		; ..loop til Done
	XOR	A		;    return Zero for Match
	RET

;.....
; Parse FileSpec addressed by HL into FN.FT Spec addressed by DE.

ChkD0:	LD	A,(HL)		; Get Char
	CP	'a'
	JR	C,ChkD1
	CP	'z'+1
	JR	NC,ChkD1
	AND	5FH		; Convert to Uppercase
ChkD1:	OR	A		; End of String?
	JR	Z,ChkDE		; ..jump if End
	INC	HL		;     (bump Inp Ptr if Not End)
	CP	'.'
	JR	Z,ChkDE		;  ..or Period field separator
	LD	(DE),A		; Store char
	INC	DE		;  bump Dest
	DJNZ	ChkD0		; ..loop til field done
ChkD2:	LD	A,(HL)		; Get Next
	OR	A
	RET	Z		;  Exit at End of string
	INC	HL		;   (adv to next)
	CP	'.'
	RET	Z		;   or field separator
	JR	ChkD2		;  ..loop til end found

ChkDE:	LD	A,' '		; Fill rest w/Spaces
ChkD3:	INC	B
	DEC	B		; More in field?
	RET	Z		; ..exit if Not
	JR	ZeroL		;  ..else stuff spaces til field ends

;.....
; Zero area addressed by DE for B Bytes.  Uses A,B,DE.

ZeroDE:	XOR	A
ZeroL:	LD	(DE),A
	INC	DE
	DJNZ	ZeroL
	RET

;.....
; Close the Directory if we just exitted a SearchF/SearchN sequence

CkSrch:	PUSH	DE		; Save Regs
	PUSH	HL
	LD	DE,(srchFD)	; Get File Desc
	LD	A,D
	OR	E		; Anything open?
	CALL	NZ,CloseV	;  Close file if Yes
	LD	HL,0
	LD	(srchFD),HL	;   Mark as closed
	POP	HL		;    (ignore Errors)
	POP	DE
	RET

;.....
; Bump current Record # for sequential R/W operations

IncCR:	LD	IY,(_arg)
	INC	(IY+32)		; Bump Lo byte
	RET	NZ
	INC	(IY+12)		; Bump Hi byte
	RET

;.....
; Init Current Record #

ZeroCR:	LD	IY,(_arg)
	LD	(IY+32),0	; Clear Lo Byte
	LD	(IY+12),0	; Clear Hi Byte
	RET

;.....
; Convert char in A to Lowercase Ascii

ChLower:
	CP	'A'
	RET	C
	CP	'Z'+1
	RET	NC
	OR	20H		; Convert to Lcase
	RET

;= = = = = = = = = = = = = = = = = = = = = = = = = = = = = =
; Bdos data in Text Segment for treating as single module

; struct fcb {
;     char drive;
;     char name[8];
;     char ext[3];
;     char junk1[4];
;     char desc;	/* This byte & 1st byte of Name used for file desc */
;     char name2[8];
;     char ext2[3];
;     char junk2[4];
;     char junk3;
; };

_arg:	DEFW	00		; Argument passed to Bdos	(char *arg;)
_call:	DEFB	0		; Bdos Function #		(char call;)
FName:	DEFM	'            '	; Storage for FCB "name" String
RName:	DEFM	'            '	; 2nd Storage for FCB "name" String (rename)

CName:	DEFM	'            '	; cached filename
	DEFB	0
Cfd:	DEFW	-1		; cached file descriptor

curFil:	DEFW	00		; Storage for File Descriptor of FCB
				;  (set by Bdos, Used by Bios)
stBuf:	DEFS	30		; Buffer for stat() results

DOSSIZ	  EQU  $-EStart
RESV	EQU	100H-(LOW DOSSIZ)
	DEFS	RESV		; Pad to make Bios start on even mult of 256

;============================================================
; The Bios Jump table MUST start on an even MOD 256 boundary
;============================================================

__bios:	JP	__cold		; 0 Cold Boot
WBoot:	JP	Exit		; 1 Warm Boot
BConSt:	JP	ConSt		; 2 Console Status
BConIn:	JP	ConIn		; 3 Console Input
BConOu:	JP	ConOut		; 4 Console Output
	JP	List		; 5 Printer Output
	JP	AuxOut		; 6 Auxiliary Output (Punch)
	JP	AuxIn		; 7 Auxiliary Input (Reader)
	JP	Home		; 8 Home drive head
	JP	SelDsk		; 9 Select Drive
	JP	SetTrk		; 10 Set Track
	JP	SetSec		; 11 Set Sector
BSDma:	JP	SetDMA		; 12 Set DMA Address
BRead:	JP	Read		; 13 Read Sector
BWrit:	JP	Write		; 14 Write Sector
	JP	ListSt		; 15 Printer Status
	JP	SecTrn		; 16 Translate Sector

;------------------------------------------------
; Cold Entry.  Set up CP/M vectors and Stack, Get
; Current TTY Parms, Save for Exit, and begin

__cold:	LD	A,0C3H
	LD	HL,__bdos
	LD	SP,HL		; Set CP/M Stack for execution
	LD	(0005H),A	;  Set Bdos Vector
	LD	(0006H),HL
	LD	HL,WBoot
	LD	(0000H),A	;   Set Bios Warm Boot Vector
	LD	(0001H),HL
	LD	HL,ttDatO	; & buf
IF UZIX
	LD	DE,TTY_RAW_UNBUFF
	CALL	IoCtl		;   Execute ioctl fcn on STDIN
	ld	(ttDatO), HL	; previous state of TTY
ELSE
; if UZI
	LD	DE,TIOCGETP	;  ioctl fcn to Get Parms
	CALL	IoCtl		;   Execute ioctl fcn on STDIN
	LD	HL,ttDatO
	LD	DE,ttDat
	LD	BC,6
	LDIR			;  Move to Work Area
		; Now we need to Change Modes defined in DEVTTY as:
		;   RAW    = 20H	(0000040)
		;   CRMOD  = 10H	(0000020)
		;   ECHO   = 08H	(0000010)
		;   CBREAK = 02H	(0000002)
		;   COOKED = 00H	(0000000)
	LD	A,(ttDat+4)
	AND	0C4H		; Kill CRMOD, ECHO, CBREAK BITS
	OR	20H		;  Set RAW Mode
	LD	(ttDat+4),A
	LD	HL,ttDat	; &buf
	LD	DE,TIOCSETP	;  ioctl Set TTY Parms Function
	CALL	IoCtl		;   Execute ioctl fcn on STDIN
	LD	DE,TIOCTLSET
	CALL	IoCtl		; Disable Control Char Processing
ENDIF
	CALL	0100H		;  ..Execute!

;.....
; 1 - Warm Boot Vector (Exits back to UZI)		{exit (0);}
;     TTY Port Settings are restored to original state.

Exit:	LD	C,0Dh
	CALL	ConOut
	LD	C,0Ah
	CALL	ConOut
	LD	HL,ttDatO	; & buf
IF UZIX
	ld	de,(ttDatO)
	CALL	IoCtl		;   Execute ioctl Fcn on STDIN
ELSE
; uzi
	LD	DE,TIOCSETP	;  ioctl fcn to Set Parms
	CALL	IoCtl		;   Execute ioctl Fcn on STDIN
	LD	DE,TIOCTLRES
	CALL	IoCtl		; Re-Enable Control Char Processing
ENDIF
	LD	HL,0		; Exit Good Status
	PUSH	HL
	LD	HL,ExitFcn	; Exit Func
	PUSH	HL		;  UZI Fcn 0 (_exit)
	RST	8H		;   Execute!
	DI
	HALT

;.....
; 2 - Return Console Input Status

ConSt:	LD	HL,cnt		; &buf
	LD	DE,TIOCSETN	;  ioctl fcn to read queue count
	CALL	IoCtl		;   Execute ioctl on STDIN
	LD	HL,(cnt)
	LD	A,H
	OR	L		; Anything There?
	RET	Z		; ..return Zero if Not
	OR	0FFH		; Else signify char waiting
	RET

;.....
; 3 - Read Console Input Char			{read (stdin, &char, 1);}

ConIn:	call	ConSt
	jr	z,ConIn
	LD	HL,1		; 1 char
	PUSH	HL
	LD	DE,char		;  Addr to put char
	PUSH	DE
	LD	L,STDIN		;   fd
	PUSH	HL
	LD	L,ReadFcn	;    UZI Read Fcn
ChrV0:	PUSH	HL
	RST	8H		;     Execute
	POP	BC
	POP	BC
	POP	BC
	POP	BC
	LD	A,(char)
	RET

;.....
; 4 - Write Char in C to Console		{write (stdout, &char, 1);}

ConOut:	LD	A,C
	LD	DE,char
	LD	(DE),A		; Stash char
	LD	HL,1		; 1 char
	PUSH	HL
	PUSH	DE		;  Addr to get char
	LD	L,STDOUT	;   fd
	PUSH	HL
	LD	L,8		;    UZI Write Fcn
	JR	ChrV0		;   ..go to common code

;.....

List:				; Bios Fcn 5
AuxOut:				; Bios Fcn 6
AuxIn:				; Bios Fcn 7
Home:				; Bios Fcn 8
SetTrk:				; Bios Fcn 10
SetSec:				; Bios Fcn 11
ListSt:				; Bios Fcn 15
SecTrn:	XOR	A		; Bios Fcn 16.  These are No-Ops
	RET

;.....
; 9 - Select Disk.  Simply return the DPH pointer

SelDsk:	LD	HL,dph		; Return DPH Pointer
	RET

;.....
; 12 - Set DMA Transfer Address

SetDMA:	LD	(dmaadr),BC	; Save Address
	Ret

;.....
; 13 - Read a "Sector" to DMA Address		{read (curFil, dmaadr, 128);}

Read:	LD	A,ReadFcn	; Set UZI Read Fcn
	CALL	RdWrt		;  Do the work
	RET	C		; ..exit if Error
	OR	A		; 0 bytes Read?
	JR	Z,XErr1		; ..Return Error if Yes (EOF)
	SUB	128		; A full 128 bytes Read?
	RET	Z		;   return Ok if Yes
	LD	DE,(dmaadr)
	ADD	HL,DE		; Else offset to byte after end
Feof:	LD	(HL),1AH	;  stuff EOF in case of text
	INC	HL
	INC	A
	JR	NZ,Feof
	RET			;   exit with OK status

;.....
; 14 - Write a "Sector" from DMA Address	{write (curFil, dmaadr, 128);}

Write:	LD	A,WriteFcn	; Set UZI Write Fcn
	CALL	RdWrt		;  Do the work
	RET	C		; ..exit if Error
	SUB	128		; Good Write?
	RET	Z		;   return Ok if Yes
XErr1:	SCF
	JR	XErr		;  Else Return Error

; Common Read/Write Support Routine

RdWrt:	LD	DE,128		; 1 "Sector" char
			; Entry Point accessed by Search Next (BDos)
RdWrt0:	PUSH	DE
	LD	HL,(dmaadr)	;  from here
	PUSH	HL
	LD	HL,(curFil)	;   to this file
	PUSH	HL
	LD	E,A		;    Position R/W Fcn #
	PUSH	DE
	RST	8H		;     Execute!
	POP	BC		; Clear Stack
	POP	BC
	POP	BC
	POP	BC
	LD	A,L		; Shuffle possible byte quantity
	RET	NC		; ..return if No Error
XErr:	LD	A,01H		; Else Signal Error (keeping Carry)
	RET

;==========================================================
;		 Bios Support Utilities
;==========================================================
; Execute ioctl Function on STDIN
; Enter: HL = Addr of Parm Block
;	 DE = ioctl Function to execute
; Exit : None
; Uses : AF,BC,DE,HL

IoCtl:	PUSH	HL		; &buf
	PUSH	DE		;  ioctl fcn
	LD	E,STDIN		;   fd
	PUSH	DE
	LD	E,IoctlFcn	;    UZI ioctl Fcn #
	PUSH	DE
	RST	8H		;     Execute!
	POP	BC		; Clean Stack
	POP	BC
	POP	BC
	POP	BC
	RET

;- - - - - - - - - - Data Structures - - - - - - - - -

dph:	DEFW	0		; Ptr to Skew Table
	DEFW	0,0,0		; Scratch Words for BDos use
	DEFW	dir		; Ptr to Directory Buffer
	DEFW	dpb		; Ptr to DPB
	DEFW	0		; Ptr to Disk Checksum Buffer
	DEFW	0		; Ptr to ALV Buffer


dpb:	DEFW	64		; Dummy Disk Parameter Block
	DEFB	4
	DEFB	15
	DEFW	0FFFFH
	DEFW	1023
	DEFB	0FFH,0
	DEFB	0,0,0,0

;----------------------- Data -----------------------

dmaadr:	DEFW	0080H		; Read/Write Transfer Addr   (char *dmaadr;)
dmaSav:	DEFW	0		; Temp storage of current DMA Address
srchFD:	DEFW	0		; File Descriptor for Searches
char:	DEFB	' '		; Byte storage for Conin/Conout
cnt:	DEFW	0		; Count of waiting keys
ttDat:	DEFB	0,0,0,0,0,0	; Working TTY Port Settings
ttDatO:	DEFB	0,0,0,0,0,0	; Initial TTY Port Settings

dir:	DEFS	128		; Directory Buffer

	DEFS	128
_Bstack:
_userSP:DEFS	2

.dephase

END

